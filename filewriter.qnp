import "outputformat.qnp"
import "fileio.qnp"
import "assembler.qnp"

import "errorhandler.qnp"

fn<u8 const*> itos(i64 num, u8* buffer, i64 base, i64 width):
    var len = std.strlen(std.itos(num, buffer, base))
    var shiftBuffer = buffer
    while len < width:
        *shiftBuffer = '0'
        ++shiftBuffer
        ++len
        
    var _ = std.itos(num, shiftBuffer, base)

    return buffer

fn<u64> genInstructionBuffer(u8* buffer, u32 instruction, OutputFormat outputFormat):
    if outputFormat == OutputFormat.bin:
        buffer[0] = (instruction >> 24) & 0xFF
        buffer[1] = (instruction >> 16) & 0xFF
        buffer[2] = (instruction >> 8) & 0xFF
        buffer[3] = (instruction >> 0) & 0xFF
        return 4
    if outputFormat == OutputFormat.bintxt:
        var<u64> len = 0
        len += std.strlen(itos((instruction >> 24) & 0xFF, buffer + len, 2, 8))
        buffer[len++] = '_'
        len += std.strlen(itos((instruction >> 16) & 0xFF, buffer + len, 2, 8))
        buffer[len++] = '_'
        len += std.strlen(itos((instruction >> 8) & 0xFF, buffer + len, 2, 8))
        buffer[len++] = '_'
        len += std.strlen(itos((instruction >> 0) & 0xFF, buffer + len, 2, 8))
        return len
    if outputFormat == OutputFormat.hextxt:
        var<u64> len = 0
        len += std.strlen(itos((instruction >> 24) & 0xFF, buffer + len, 16, 2))
        buffer[len++] = '_'
        len += std.strlen(itos((instruction >> 16) & 0xFF, buffer + len, 16, 2))
        buffer[len++] = '_'
        len += std.strlen(itos((instruction >> 8) & 0xFF, buffer + len, 16, 2))
        buffer[len++] = '_'
        len += std.strlen(itos((instruction >> 0) & 0xFF, buffer + len, 16, 2))
        return len

    handleMsgError("Invalid output format!")
    return 0

fn<bool> writeToFile(std.String const* filename, AssemblerData const* data, OutputFormat outputFormat):
    var file = std.new(std.File, filename->raw(), "w")
    if file == null:
        return handleMsgError("Could not open output file for writing!")
    
    var<u64> i = 0
    var<u8[64]> buffer

    var<u32> fileSize = 4 + (data->nInstructions * 4)

    if outputFormat == OutputFormat.bin:
        buffer[0] = (fileSize >> 24) & 0xFF
        buffer[1] = (fileSize >> 16) & 0xFF
        buffer[2] = (fileSize >> 8) & 0xFF
        buffer[3] = (fileSize >> 0) & 0xFF
        if file->write(buffer, 4) != 4:
            std.delete(file)
            return handleMsgError("Could not write to output file!")

    while i < data->nInstructions:
        var bufferSize = genInstructionBuffer(buffer, data->instructions[i], outputFormat)
        if bufferSize == 0:
            std.delete(file)
            return false

        if file->write(buffer, bufferSize) != bufferSize:
            std.delete(file)
            return handleMsgError("Could not write to output file!")

        if data->addAnnotations && outputFormat != OutputFormat.bin:
            static annotationBegin = "    // "
            static annotationBeginLength = std.strlen(annotationBegin)
            var annotation = data->annotations[i]
            var annotationLength = annotation->length()
            if file->write(annotationBegin, annotationBeginLength) != annotationBeginLength:
                std.delete(file)
                return handleMsgError("Could not write to output file!")

            if file->write(annotation->raw(), annotationLength) != annotationLength:
                std.delete(file)
                return handleMsgError("Could not write to output file!")
        
        if outputFormat != OutputFormat.bin && file->write("\n", 1) != 1:
            std.delete(file)
            return handleMsgError("Could not write to output file!")
        ++i

    std.delete(file)

    return true