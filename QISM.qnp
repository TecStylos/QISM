\\ Usage: qism [options*] <input_file>
\\
\\ Options:
\\   -h                  show this help message and exit
\\   -o=<output_file>    output file name
\\                        default: <input_file>.bin
\\   -v                  verbose mode
\\   -f=<format>         output format
\\                        default: bin
\\                        options: bintxt, hextxt
\\   -a                  add annotations to output file
\\                       only works with bintxt and hextxt formats

\\ Registers:
\\   zero                zero register
\\   gp                  global pointer
\\   sp                  stack pointer
\\   bp                  base pointer
\\   ra                  return address
\\   r0-r10              general purpose registers

\\ Instructions:
\\   add [dest] [src1]* [src2]*
\\   sub [dest] [src1]* [src2]*
\\   and [dest] [src1]* [src2]*
\\   or  [dest] [src1]* [src2]*
\\   xor [dest] [src1]* [src2]*
\\   jxx [addr]* [src]*
\\   st  [addr]* [src]*
\\   ld  [dest] [addr]*
\\   lui [dest] [imm]
\\   lli [dest] [imm]
\\   li  [dest] [imm]
\\
\\  ++ '*' notates that one of these arguments may have an offset. Example:
\\     add r0 r1 r2(4)
\\
\\  ++ 'xx' of 'jxx' is one of the following:
\\     - '.z'    zero
\\     - '.nz'   not zero
\\     - '.gz'   greater than zero
\\     - '.lz'   less than zero
\\     - '.ge'   greater than or equal to zero
\\     - '.le'   less than or equal to zero
\\     Where '.' is either 'r' (relative jump) or '' (absolute jump).
\\
\\ Comments:
\\   ~~ comment
\\
\\ Labels:
\\  ::label
\\
\\ Entry Point:
\\  .entry <label>

import "memory.qnp"
import "string.qnp"
import "stdio.qnp"
import "system.qnp"
import "fileio.qnp"
import "assembler.qnp"
import "filewriter.qnp"

\\ Read a file into a string.
fn<std.String*> readFile(std.String const* filename):
    var file = std.new(std.File, filename->raw(), "r")
    if file == null:
        handleMsgError("Could not open file for reading!")
        return null

    var size = file->size()

    if size == std.limits.U64_MAX:
        handleMsgError("Could not get file size!")
        std.delete(file)
        return null

    var buffer = (u8*)std.malloc(size + 1)
    if buffer == null:
        handleMsgError("Could not allocate memory for file reading!")
        std.delete(file)
        return null

    var nRead = file->read(buffer, size)
    std.delete(file)
    if nRead != size:
        handleMsgError("Could not read file!")
        std.free(buffer)
        return null
    
    buffer[size] = 0

    var str = std.new(std.String, buffer)
    if str == null:
        handleMsgError("Could not allocate memory for file reading!")
        std.free(buffer)
        return null

    return str

\\ Print help message.
fn<> printHelp():
    std.println("Usage: qism [options*] <input_file>")
    std.println()
    std.println("Options:")
    std.println("  -h                  show this help message and exit")
    std.println("  -o=<output_file>    output file name")
    std.println("                       default: <input_file>.bin")
    std.println("  -v                  verbose mode")
    std.println("  -f=<format>         output format")
    std.println("                       default: bin")
    std.println("                       options: bintxt, hextxt")

\\ Option variables
var<std.String*> inputFilename = null
var<std.String*> outputFilename = null
var<OutputFormat> outputFormat = OutputFormat.bin
var<bool> verbose = false
var<bool> addAnnotations = false

var<u8* const*> argv
var<u64> argc = std.getargs(&argv)

\\ Parse options
var argi = 0
while argv[++argi] != null:
    var arg = std.new(std.String, (u8 const*)argv[argi])

    if arg->equals("-h"):
        printHelp()
        std.delete(arg)
        std.exit(0)
    elif arg->beginswith("-o="):
        outputFilename = std.new(std.String, arg->raw() + 3)
        std.delete(arg)
        continue
    elif arg->equals("-v"):
        verbose = true
        std.delete(arg)
        continue
    elif arg->beginswith("-f="):
        if arg->equals("-f=bin"):
            outputFormat = OutputFormat.bin
        elif arg->equals("-f=bintxt"):
            outputFormat = OutputFormat.bintxt
        elif arg->equals("-f=hextxt"):
            outputFormat = OutputFormat.hextxt
        else:
            std.println("error: invalid output format!")
            std.delete(arg)
            std.exit(1)
        std.delete(arg)
        continue
    elif arg->equals("-a"):
        addAnnotations = true
        std.delete(arg)
        continue
    
    if inputFilename == null:
        if argv[argi + 1] != null:
            std.println("error: unrecognized option!")
            std.delete(arg)
            std.exit(1)

        inputFilename = arg
        continue

    std.println("error: too many arguments!")
    std.delete(arg)
    std.exit(1)

\\ Check if input file was specified
if inputFilename == null:
    std.println("error: no input file!")
    std.exit(1)

\\ If output file was not specified, use input file name with .bin extension
if outputFilename == null:
    outputFilename = std.new(std.String, inputFilename->raw())
    outputFilename->append(".bin")

\\ Print option values if verbose
if verbose:
    std.println("Selected options:")
    std.println("  Input file:      %", inputFilename->raw())
    std.println("  Output file:     %", outputFilename->raw())
    std.println("  Output format:   % (%)", (i64)outputFormat, getOutputFormatName(outputFormat))
    std.println("  Verbose:         %", verbose)
    std.println("  Add annotations: %", addAnnotations)


if verbose: std.println("Reading input file...")
var codeText = readFile(inputFilename)
if codeText == null:
    std.delete(inputFilename)
    std.delete(outputFilename)
    std.exit(1)

if verbose: std.println("Assembling...")
var<AssemblerData> asmData
asmData.filename = inputFilename
asmData.code = codeText
asmData.verbose = verbose
asmData.addAnnotations = addAnnotations
if !assemble(&asmData):
    std.delete(codeText)
    std.delete(inputFilename)
    std.delete(outputFilename)
    std.exit(1)

if verbose: std.println("Writing output file...")
if !writeToFile(outputFilename, &asmData, outputFormat):
    std.delete(codeText)
    std.delete(inputFilename)
    std.delete(outputFilename)
    freeAsmData(&asmData)
    std.exit(1)


\\ Cleanup
if verbose: std.println("Cleaning up...")
std.free(codeText)
std.delete(inputFilename)
std.delete(outputFilename)
freeAsmData(&asmData)

if verbose: std.println("Done!")
std.exit(0)